plugins {
   id 'java'
   id 'com.github.johnrengelman.shadow' version '7.1.2'
}

dependencies {
   // common lib deps
   implementation project(':common')
   implementation 'com.eclipsesource.minimal-json:minimal-json:0.9.5'
   implementation 'me.carleslc.Simple-YAML:Simple-Yaml:1.8'

   // graal deps
   implementation "org.graalvm.js:js:${graal_version}"
   implementation "org.graalvm.sdk:graal-sdk:${graal_version}"
   implementation "org.graalvm.truffle:truffle-api:${graal_version}"

   // platform-specific deps
   compileOnly "org.spigotmc:spigot-api:1.8-R0.1-SNAPSHOT"
}

jar {
   archiveBaseName.set('scriptcraft2')
   archiveClassifier.set('spigot')
   archiveVersion.set(project.property('scriptcraft2_version'))
}

shadowJar {
   mergeServiceFiles()
}

processResources {
   filesMatching('**/plugin.yml') {
      expand project.properties
   }
}

repositories {
   mavenCentral()
   maven { url = 'https://jitpack.io' }
   maven { url = 'https://oss.sonatype.org/content/repositories/central' }
   maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
   maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}
